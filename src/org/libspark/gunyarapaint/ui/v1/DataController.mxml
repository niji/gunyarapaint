<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="400" height="346"
                title="ふっかつのじゅもん" showCloseButton="true" close="onClose()" x="10" y="10">
    <mx:Script>
        <![CDATA[
            import mx.controls.Alert;
            import mx.core.Application;
            import mx.managers.PopUpManager;
            import mx.utils.Base64Decoder;
            import mx.utils.Base64Encoder;
            
            private static const ALERT_TITLE:String = "ふっかつのじゅもん";
            
            public function set password(value:String):void
            {
                passwordTextArea.text = value;
            }
            
            public function onClose():void
            {
                PopUpManager.removePopUp(this);
            }
            
            public function onLoad():void
            {
                var bytes:ByteArray = null;
                var password:String = passwordTextArea.text;
                var decoder:Base64Decoder = new Base64Decoder();
                // ※ネタです
                if (password == "ザオリク" || password == "ざおりく") {
                    Alert.show("かんちがいしています", ALERT_TITLE);
                }
                else {
                    decoder.decode(password);
                    bytes = decoder.toByteArray();
                    try {
                        Application.application.load(bytes);
                        PopUpManager.removePopUp(this);
                    }
                    catch (e:Error) {
                        trace(e.message);
                        Alert.show("じゅもんがちがいます", ALERT_TITLE);
                    }
                }
            }
            
            private function onSave():void
            {
                var bytes:ByteArray = new ByteArray();
                var encoder:Base64Encoder = new Base64Encoder();
                Application.application.save(bytes);
                encoder.encodeBytes(bytes);
                passwordTextArea.text = encoder.toString();
            }
            
            private function onClipboard():void
            {
                System.setClipboard(passwordTextArea.text);
                Alert.show("ふっかつのじゅもんをクリップボードにコピーしました", ALERT_TITLE);
            }
        ]]>
    </mx:Script>
    <mx:TextArea id="passwordTextArea" x="0" y="0" width="380" height="266" verticalScrollPolicy="on"/>
    <mx:Button id="loadButton" x="10" y="274" label="ふっかつする" click="onLoad()"/>
    <mx:Button id="setClipboardButton" x="124.5" y="274" label="クリップボードにコピー" click="onClipboard()"/>
    <mx:Button id="saveButton" x="290" y="274" label="ほぞんする" click="onSave()"/>
</mx:TitleWindow>
