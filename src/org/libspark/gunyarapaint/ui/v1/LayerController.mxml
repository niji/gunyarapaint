<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute"
                width="144" height="360"
                implements="org.libspark.gunyarapaint.ui.v1.IController"
                xmlns:ui="org.libspark.gunyarapaint.ui.v1.*">
    <mx:Script>
        <![CDATA[
            import mx.collections.ArrayCollection;
            import mx.controls.Alert;
            import mx.core.Application;
            import mx.events.DragEvent;
            import mx.events.FlexEvent;
            import mx.events.ListEvent;
            import mx.events.SliderEvent;
            
            import org.libspark.gunyarapaint.framework.LayerBitmap;
            import org.libspark.gunyarapaint.framework.LayerBitmapCollection;
            import org.libspark.gunyarapaint.framework.Painter;
            import org.libspark.gunyarapaint.framework.UndoStack;
            import org.libspark.gunyarapaint.framework.events.UndoEvent;
            import org.libspark.gunyarapaint.framework.ui.IApplication;
            import org.libspark.gunyarapaint.utils.ComponentResizer;
            
            public function init(app:IApplication):void
            {
                var layers:LayerBitmapCollection = app.layers;
                var currentLayerIndex:uint = layers.currentIndex;
                var currentLayerBlendMode:String = layers.at(currentLayerIndex).blendMode;
                var blendModes:Object = blendModeComboBox.dataProvider;
                var blendModeLength:uint = blendModes.length;
                var undo:UndoStack = app.undoStack;
                undo.addEventListener(UndoEvent.UNDO, onChangeUndo);
                undo.addEventListener(UndoEvent.REDO, onChangeUndo);
                blendModeComboBox.dataProvider = app.supportedBlendModes;
                layerDataGrid.selectedIndex = getSelectedIndex(layers);
                alphaSlider.value = layers.at(currentLayerIndex).alpha;
                for (var i:uint = 0; i < blendModeLength; i++) {
                    if (blendModes[i].data == currentLayerBlendMode)
                        blendModeComboBox.selectedIndex = i;
                }
                layerDataGrid.addEventListener("describeChange", itemCheckChangeHandler);
                ComponentResizer.addResize(this, new Point(144, 230));
                m_initPosition = new Point(x, y);
                update();
            }
            
            public function load(data:Object):void
            {
                var point:Object = data.point;
                move(point.x, point.y);
                update();
            }
            
            public function save(data:Object):void
            {
                data.point = new Point(x, y);
            }
            
            public function resetWindow():void
            {
                move(m_initPosition.x, m_initPosition.y);
            }
            
            public function update():void
            {
                var layers:LayerBitmapCollection = IApplication(Application.application).layers;
                var layer:LayerBitmap = layers.at(layers.currentIndex);
                var currentLayerBlendMode:String = layer.blendMode;
                layerDataGrid.dataProvider = layers.toDataProvider();
                layerDataGrid.selectedIndex = getSelectedIndex(layers);
                alphaSlider.value = layer.alpha;
                var ac:ArrayCollection = blendModeComboBox.dataProvider as ArrayCollection;
                var length:uint = ac.length;
                for (var i:uint = 0; i < length; i++) {
                    if (ac.getItemAt(i).data == currentLayerBlendMode) {
                        blendModeComboBox.selectedIndex = i;
                        return;
                    }
                }
            }
            
            private function onClickLayer(evt:ListEvent):void
            {
                var app:IApplication = IApplication(Application.application);
                var layer:LayerBitmap = LayerBitmap(evt.currentTarget.selectedItem);
                if (app.layers.currentIndex != layer.index)
                    app.canvasModule.layerIndex = layer.index;
            }
            
            private function onCreateLayer(evt:Event):void
            {
                try {
                    // should catch AddLayerError here
                    var app:IApplication = IApplication(Application.application);
                    app.canvasModule.createLayer();
                    update();
                } catch (e:Error) {
                    Alert.show(e.message, title);
                }
            }
            
            private function onCopyLayer(evt:Event):void
            {
                try {
                    // should catch AddLayerError here
                    var app:IApplication = IApplication(Application.application);
                    app.canvasModule.copyLayer();
                    update();
                } catch (e:Error) {
                    Alert.show(e.message, title);
                }
            }
            
            private function onRemoveLayer(evt:Event):void
            {
                try {
                    // should catch RemoveLayerError here
                    var app:IApplication = IApplication(Application.application);
                    app.canvasModule.removeLayer();
                    update();
                } catch (e:Error) {
                    Alert.show(e.message, title);
                }
            }
            
            private function onMergeLayers(evt:Event):void
            {
                try {
                    // should catch MergeLayersError here
                    var app:IApplication = IApplication(Application.application);
                    app.canvasModule.mergeLayers();
                    update();
                } catch (e:Error) {
                    Alert.show(e.message, title);
                }
            }
            
            private function itemCheckChangeHandler(evt:Event):void
            {
                // do nothing...
            }
            
            private function onChangeAlphaSlider(evt:SliderEvent):void
            {
                var app:IApplication = IApplication(Application.application);
                app.canvasModule.layerAlpha = evt.value;
            }
            
            private function onSelectBlendMode(evt:ListEvent):void
            {
                var app:IApplication = IApplication(Application.application);
                app.canvasModule.layerBlendMode = String(evt.currentTarget.value);
            }
            
            private function onChangeUndo(event:UndoEvent):void
            {
                update();
            }
            
            private function onDragComplete(evt:DragEvent):void
            {
                var app:IApplication = IApplication(Application.application);
                var a:Array = ArrayCollection(layerDataGrid.dataProvider).toArray().reverse();
                var length:uint = a.length;
                var layers:LayerBitmapCollection = app.layers;
                for (var i:uint = 0; i < length; i++) {
                    var from:uint = a[i].index;
                    var to:uint = layers.at(i).index;
                    if (a[i].index != to) {
                        app.canvasModule.swapLayers(from, to);
                        break;
                    }
                }
            }
            
            private function getSelectedIndex(layers:LayerBitmapCollection):uint
            {
                return layers.count - layers.currentIndex - 1;
            }
            
            private var m_initPosition:Point;
        ]]>
    </mx:Script>
    <ui:ExtendedDataGrid id="layerDataGrid" x="0" y="43" width="124" height="200" editable="true" sortableColumns="false" dragMoveEnabled="true" dragEnabled="true" dropEnabled="true"
                         itemClick="onClickLayer(event)" dragComplete="onDragComplete(event)">
        <ui:columns>
            <mx:DataGridColumn editable="false" headerText="{_('The layer is visible')}" dataField="visible" itemRenderer="org.libspark.gunyarapaint.ui.v1.CheckBoxEditor" rendererIsEditor="true" width="20"/>
            <mx:DataGridColumn editable="false" headerText="{_('The layer is locked')}" dataField="locked" itemRenderer="org.libspark.gunyarapaint.ui.v1.CheckBoxEditor" rendererIsEditor="true" width="20"/>
            <!--mx:DataGridColumn headerText="{_('Mask the layer')}" dataField="maskCheck" itemRenderer="org.libspark.gunyarapaint.controls.CheckBoxEditor" rendererIsEditor="true" width="20"/-->
            <mx:DataGridColumn editable="true" headerText="{_('The layer name')}" dataField="name"/>
        </ui:columns>
    </ui:ExtendedDataGrid>
    <mx:Button x="6" y="246" id="newLayerButton" label="{_('Create a new layer')}" width="22" paddingLeft="0" paddingRight="0" buttonDown="onCreateLayer(event)"/>
    <mx:Button x="36" y="246" id="copyLayerButton" label="{_('Copy the layer')}" width="22" paddingLeft="0" paddingRight="0" buttonDown="onCopyLayer(event)"/>
    <mx:Button x="66" y="246" id="deleteLayerButton" label="{_('Delete the layer')}" width="22" paddingLeft="0" paddingRight="0" buttonDown="onRemoveLayer(event)"/>
    <mx:Button x="96" y="246" id="mergeLayerButton" label="{_('Merge the layer')}" width="22" paddingLeft="0" paddingRight="0" buttonDown="onMergeLayers(event)"/>
    <mx:ComboBox id="blendModeComboBox" x="0" y="0" width="124" change="onSelectBlendMode(event)"></mx:ComboBox>
    <mx:Label x="5" y="23" text="{_('Transparency of the layer')}"/>
    <mx:HSlider id="alphaSlider" x="18" y="20" width="99" minimum="0.05" maximum="1" snapInterval="0.05" value="1" change="onChangeAlphaSlider(event)"/>
    <mx:Button id="horizontalMirrorButton" label="{_('Mirror horizontally to all layers')}" width="117" height="17" buttonDown="IApplication(Application.application).canvasModule.horizontalMirror(Painter.ALL_LAYERS)" y="273" x="3"/>
    <mx:Button id="verticalMirrorButton" label="{_('Mirror vertically to all layers')}" width="117" height="17" buttonDown="IApplication(Application.application).canvasModule.verticalMirror(Painter.ALL_LAYERS)" x="3" y="295"/>
</mx:TitleWindow>
